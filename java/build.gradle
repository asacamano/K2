// K2 Crypto Library Java Build File
//
// Copyright 2016 Google. Inc.
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
// http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

// Allow the build process to pull plugins from maven central
// and run the proto compiling plugin
buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    // See the spotless section below
    classpath 'com.diffplug.gradle.spotless:spotless:1.3.3'
  }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'com.diffplug.gradle.spotless'

// Set up a master project with useful links
eclipse {
  classpath {
	  //default settings for downloading sources and Javadoc:
	  downloadSources = true
    downloadJavadoc = true
  }
  project {
    name = 'K2 Java Master'
    // linkedResource name: 'protos', type: '2', locationUri: 'file://../protos'
  }
}

subprojects {
  apply plugin: 'java'
  apply plugin: 'eclipse'
  // See the spotless section below
  apply plugin: 'com.diffplug.gradle.spotless'

  // The standard group for all projects
  group = 'com.google.k2'

  // Set to Java 1.6  to support Android 4.4
  // Older Java versions are well past their EOL:
  // http://www.oracle.com/technetwork/java/eol-135779.html
  //
  // And don't seem to be used much anymore:
  // https://plumbr.eu/blog/java/java-version-statistics-2015-edition
  sourceCompatibility = 1.6
  targetCompatibility = 1.6

  // Use mavenCentral for project dependencies
  repositories {
    mavenCentral()
  }

  // Common dependencies for testing
  dependencies {
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-all:1.10.19'
  }

  // Common eclipse settings
  eclipse {
    classpath {
	    //default settings for downloading sources and Javadoc:
	    downloadSources = true
	    downloadJavadoc = true
    }
  }

  // Enforce code style using the Spotless plugin
  spotless {
    java {
	target project.fileTree(project.rootDir) {
		include '**/*.java'
		exclude 'protos-java/build/proto-java/**/*.*'
	}
      eclipseFormatFile rootProject.file('code-style/eclipse-java-google-style.xml')
      importOrderFile   rootProject.file('code-style/eclipse.importorder')
      trimTrailingWhitespace()
      endWithNewline()
    }
    freshmark {
      trimTrailingWhitespace()
      indentWithSpaces(2)
      endWithNewline()
    }
    format 'misc', {
      target '**/*.gradle', '**/*.sh', '**/.gitignore'
      indentWithSpaces(2)
      trimTrailingWhitespace()
      endWithNewline()
    }
  }
}

